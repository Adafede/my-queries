#+ summary: Chemicals treating Gram-positive infections
#+ description: What are the chemicals known to treat gram-positive infections (Q55789782)?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Chemicals

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#title: What are the chemicals known to treat gram-positive infections (Q55789782)?
SELECT DISTINCT ?structure ?structure_smiles WHERE {
    VALUES ?disease {
        wd:Q55789782   # Gram-positive
    # wd:Q19597369 # Gram-negative
    }
    ?infection (wdt:P279*) ?disease.
    { ?structure wdt:P2175 ?infection. }
    UNION
    { ?infection wdt:P2176 ?structure. }
    ?structure wdt:P233 ?structure_smiles.
}
