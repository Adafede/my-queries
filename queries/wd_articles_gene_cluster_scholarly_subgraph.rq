#+ summary: Scholarly articles containing 'gene cluster' and found in taxon
#+ description: Scholarly articles containing 'gene cluster' and found in taxon
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Scholarly articles

PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Scholarly articles containing 'gene cluster' (Q1501261) and NPs
SELECT DISTINCT ?article ?article_doi ?article_title WHERE {
    SERVICE wikibase:mwapi {
        bd:serviceParam wikibase:endpoint "www.wikidata.org";
        wikibase:api "Search";
        mwapi:srsearch "gene cluster haswbstatement:P31=Q13442814";
        mwapi:srlimit "max".
        ?article wikibase:apiOutputItem mwapi:title.
    }
    SERVICE <https://query-scholarly.wikidata.org/sparql> {
      ?article wdt:P356 ?article_doi.
      ?article wdt:P1476 ?article_title.
    }
    ?s (p:P703/prov:wasDerivedFrom/pr:P248) ?article. hint:Prior hint:rangeSafe TRUE.
    ?s p:P235 [].
}
